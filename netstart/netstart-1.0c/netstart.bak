#! /bin/tcsh -f

#
# NetStart v. 1.0c
#
# VERSION: 26 May 2003, K. Rapacki
#

# *****************************************************************************
# ******* *******            EDIT TO SUIT YOUR SITE             ******* *******

# full path to the NetStart-1.0 directory (mandatory)
setenv	NSHOME		/usr/cbs/packages/netstart/1.0c/netstart-1.0c

# determine platform (do not change this unless you don't have 'uname'!)
set	SYSTEM		= `uname -s`

# select AWK executable
if ( $SYSTEM == "IRIX" ) then
   setenv AWK /usr/bin/nawk
else if ( $SYSTEM == "IRIX64" ) then
   setenv AWK /usr/bin/nawk
else if ( $SYSTEM == "SunOS" ) then
   setenv AWK /bin/nawk
else if ( $SYSTEM == "Linux" ) then
   setenv AWK /usr/bin/gawk
else if ( $SYSTEM == "OSF1" ) then
   setenv AWK /bin/nawk
endif
# override the above setting with your own terrific AWK ;-)
#setenv AWK     mysuperawk

# ECHO: avoid using the shell built-in echo command
#setenv	ECHO		/usr/bin/echo
if ( $SYSTEM == "Linux" ) then
   setenv ECHO echo
else
   setenv ECHO /bin/echo
endif

# ******* *******      NO CHANGES EXPECTED BELOW THIS LINE      ******* *******
# *****************************************************************************

setenv	NSBIN		$NSHOME/bin
setenv	NSETC		$NSHOME/etc
setenv	NSSYN		$NSHOME/syn
setenv	NSTMP		$NSHOME/tmp/$$

set	t_opt		= vert		# default: vertebrate sequences
set	infile				# default: stdin
set	ending		= s		# default: plural ending


# command line ================================================================
while ( $1 != "" )
	switch ($1)
	case "-at":
		set t_opt = at
	breaksw
	case "-h":
		set h_opt
	breaksw
	case "-vert":
		set t_opt = vert
	breaksw
	default:
		set infile = ($infile $1)
	breaksw
	endsw

	shift
 
end

if ( $?h_opt ) then					# help on usage
	$ECHO usage: netstart \[-at\|-vert\] \[\<file\> ...\]
	exit 0

else if ( $#infile ) then                               # check infile(s)
        foreach f ($infile)
                if ( ! -e $f ) then     # infile not found
                        echo netstart: \"$f\" not found
                        exit -1
                endif
        end
endif

mkdir $NSTMP						# Ol Korrect ...


# prepare input file in HOW format ============================================
(cat $infile | $AWK -f $NSBIN/fasta2how >$NSTMP/infile) >&/dev/null

set	infile	= $NSTMP/infile
@	ecount	= `grep '[0-9] [^ ]' $infile | wc -l`

if ( $ecount == 0 ) then
	exit 0
else if ( $ecount == 1 ) then
	set ending
endif


# MAIN ========================================================================
cd $NSTMP

if ( $t_opt == "at" ) then
	set type = 'dicot plant'
else
	set type = vertebrate
endif

set nshome = `$ECHO $NSHOME | sed 's/\//\\\//g'`	# escape '/':s in name

$ECHO '\n'Translation start predictions for $ecount $type sequence$ending

cat $NSETC/par.how | \
	sed 's/__NUM__/'$ecount'/;s/__SYN__/'$t_opt'/;s/__NS__/'$nshome'/' | \
	$NSBIN/how98_`uname` | $AWK -f $NSBIN/pp


# cleanup =====================================================================
cd /
rm -r $NSTMP


# end of script ===============================================================
