#! /usr/freeware/bin/gawk -f

# netstart-1.0b/bin/pp - postprocessing HOW output
# 
# This script reads NetStart 1.0b HOW output and prints the final output.
# 
# Note:	to debug this script comment out its use in the main netstart script
# 	and examine the HOW output directly.
# 
# VERSION:	10 Sep 2002, K. Rapacki
# 

/^ SINGLE WINDOW / { 	next; }
/^ T\*SAMPLE\*/ {	exit;}


/^ TEST SEQUENCES:/ {	active = 1; next;}

# skip everything while not active ............................................
!active {		next; }

# print sequence name .........................................................
/^ #/ {			print "\n\n  Name: " substr($2,index($2,":")+1);
			noheader = 1;
			next; }

# skip empty lines ............................................................
/^$/ {			next; }

# print sequence ..............................................................
/^ [A-Z][A-Z]/ {	print $1;
			next;
}

# print assignment ............................................................
/^ [ iN] / {		if ( ! match($0,"[1-9]") ) {
			   gsub(" ",".",$0);
			   print substr($0,2);
			   next;
			}
}

# print scores ................................................................
/^ *[1-9]/ {		if (noheader) {
			   print "\n   Pos    Score     Pred";
			   print "------------------------";
			   noheader=0;
			}

			if ($NF == 0) next;
			score = $(NF-1)/($(NF-1)+$NF);
			if (score > 0.5 )
			   printf("%6d    %5.3f     Yes\n",$1,score);
			else
			   printf("%6d    %5.3f     -\n",$1,score);
}
